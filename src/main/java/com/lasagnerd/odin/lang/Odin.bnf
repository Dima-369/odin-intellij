{
  parserImports="static com.intellij.lang.parser.GeneratedParserUtilBase.*"
  parserClass="com.lasagnerd.odin.lang.OdinParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"


  psiClassPrefix="Odin"
  psiImplClassSuffix="Impl"

  psiPackage="com.lasagnerd.odin.lang.psi"
  psiImplPackage="com.lasagnerd.odin.lang.psi.impl"

  elementTypeHolderClass="com.lasagnerd.odin.lang.psi.OdinTypes"
  elementTypeClass="com.lasagnerd.odin.lang.psi.OdinElementType"
  tokenTypeClass="com.lasagnerd.odin.lang.psi.OdinTokenType"
  parserUtilClass="com.lasagnerd.odin.lang.OdinParserUtil"

  tokens=[
        LINE_COMMENT='regexp://[^\r\n]*'
        BLOCK_COMMENT='regexp:/\*[\s\S]*?\*/'

        PACKAGE='regexp:package'
        IMPORT='regexp:import'
        PROC='regexp:proc'
        RETURN='regexp:return'
        DEFER='regexp:defer'
        STRUCT='regexp:struct'
        FOR='regexp:for'
        IN='regexp:in'
        NOT_IN='regexp:not_in'
        DO='regexp:do'
        IF_TOKEN='regexp:if'
        ELSE_TOKEN='regexp:else'
        SWITCH='regexp:switch'
        CASE='regexp:case'
        FALLTHROUGH='regexp:fallthrough'
        TRUE='regexp:true'
        FALSE='regexp:false'
        WHEN='regexp:when'
        BREAK='regexp:break'
        CONTINUE='regexp:continue'
        NIL='regexp:nil'
        TRIPLE_DASH='regexp:---'
        OR_ELSE='regexp:or_else'
        OR_RETURN='regexp:or_return'
        OR_BREAK='regexp:or_break'
        OR_CONTINUE='regexp:or_continue'
        FOREIGN='regexp:foreign'
        CAST='regexp:cast'
        AUTO_CAST='regexp:AUTO_CAST'
        TRANSMUTE='regexp:TRANSMUTE'
        DYNAMIC='regexp:dynamic'
        BIT_SET='regexp:bit_set'
        MAP='regexp:map'
        USING='regexp:using'
        ENUM='regexp:enum'
        UNION='regexp:union'
        DISTINCT='regexp:distinct'
        WHERE='regexp:where'

        IDENTIFIER_TOKEN='regexp:[a-zA-Z_][a-zA-Z0-9_]*'
        DQ_STRING_LITERAL='regexp:"[^"]*"'
        SQ_STRING_LITERAL="regexp:'[^']+?'"
        RAW_STRING_LITERAL='regexp:`[^`]*`'
        INTEGER_DEC_LITERAL='regexp:[0-9][0-9_]*'
        INTEGER_HEX_LITERAL='regexp:0x[0-9a-fA-F][0-9a-fA-F_]*'
        INTEGER_OCT_LITERAL='regexp:0o[0-7][0-7_]*'
        INTEGER_BIN_LITERAL='regexp:0b[01][01_]*'
        FLOAT_DEC_LITERAL='regexp:[0-9][0-9_]*\.[0-9][0-9_]*'
        COMPLEX_FLOAT_LITERAL='regexp:[0-9][0-9_]*\.[0-9][0-9_]*[ijk]'
        COMPLEX_INTEGER_DEC_LITERAL='regexp:[0-9][0-9_]*[ijk]'

        EQ='regexp:='
        COLON='regexp::'

        LBRACE='regexp:\{'
        RBRACE='regexp:}'
        LPAREN='regexp:\('
        RPAREN='regexp:\)'

        LBRACKET='regexp:\['
        RBRACKET='regexp:]'
        HASH='regexp:#'

        DOT='regexp:\.'
        ARROW='regexp:->'
        COMMA='regexp:,'
        SEMICOLON='regexp:;'
        QUESTION='regexp:\?'
        CARET='regexp:\^'
        AT='regexp:@'
        DOLLAR='regexp:\$'
        DOT_QUESTION='regexp:\.\?'

        // Operators
        EQEQ='regexp:=='
        NEQ='regexp:!='
        LT='regexp:<'
        GT='regexp:>'
        LTE='regexp:<='
        GTE='regexp:>='

        // Boolean operators
        ANDAND='regexp:&&'
        OROR='regexp:\|\|'
        NOT='regexp:!'
        ANDNOT='regexp:&~'


        // Arithmetic operators
        PLUS='regexp:\+'
        MINUS='regexp:-'
        STAR='regexp:\*'
        DIV='regexp:/'
        MOD='regexp:%'
        REMAINDER='regexp:%%'

        // Bitwise operators
        AND='regexp:&'
        PIPE='regexp:\|'
        TILDE='regexp:~'
        LSHIFT='regexp:<<'
        RSHIFT='regexp:>>'


        // Assignment operators
        PLUS_EQ='regexp:\+='
        MINUS_EQ='regexp:-='
        STAR_EQ='regexp:\*='
        DIV_EQ='regexp:/='
        MOD_EQ='regexp:%='
        REMAINDER_EQ='regexp:%%='
        AND_EQ='regexp:&='
        OR_EQ='regexp:\|='
        XOR_EQ='regexp:~='
        ANDAND_EQ='regexp:&&='
        OROR_EQ='regexp:\|\|='
        LSHIFT_EQ='regexp:<<='
        RSHIFT_EQ='regexp:>>='
        BITWISE_AND_NOT_EQ='regexp:&~='

        // Range Operators
        RANGE = 'regexp:\.\.'
        RANGE_INCLUSIVE='regexp:\.\.='
        RANGE_EXCLUSIVE='regexp:\.\.<'

        WHITE_SPACE='regexp:[ \t\f]+'
        NEW_LINE='regexp:(\r|\n|\r\n)+'
        EOS_TOKEN='EOS_TOKEN'

        MULTILINE_BLOCK_COMMENT='MULTILINE_BLOCK_COMMENT'

        BLOCK_COMMENT_CONTENT='BLOCK_COMMENT_CONTENT'
        BLOCK_COMMENT_START='BLOCK_COMMENT_START'
        BLOCK_COMMENT_END='BLOCK_COMMENT_END'

        DQ_STRING_START='DQ_STRING_START'
        DQ_STRING_END='DQ_STRING_END'

        SQ_STRING_START='SQ_STRING_START'
        SQ_STRING_END='SQ_STRING_END'
  ]

  extends('.*_expression') = expression
  extends('.*[sS]tatement') = statement
  extends('.*Type$') = type_expression

  methods('.*([iI]nitialization|[dD]eclaration).*') = [getDeclaredIdentifiers]
  implements('.*([iI]nitialization|[dD]eclaration).*') = 'com.lasagnerd.odin.lang.psi.OdinDeclaration'

  psiImplUtilClass="com.lasagnerd.odin.lang.psi.OdinPsiUtil"
}



odinFile                             ::= fileScope
fileScope                            ::= packageDeclaration eos importStatements fileScopeStatementList <<eof>> {
    methods=[getImportStatements]
}
private importStatements             ::= (importDeclarationStatement eos)*
private fileScopeStatementList       ::= (fileScopeStatement eos)*
packageDeclaration                   ::= PACKAGE declaredIdentifier
importDeclarationStatement           ::= IMPORT declaredIdentifier? DQ_STRING_LITERAL {methods=[alias="/declaredIdentifier" path="/DQ_STRING_LITERAL" getName setName getNameIdentifier getImportInfo] implements="com.intellij.psi.PsiNameIdentifierOwner"}
doubleColonOperator                  ::= COLON COLON

fileScopeStatement                   ::= foreignImportDeclarationStatement
                                         | importDeclarationStatement
                                         | enumDeclarationStatement
                                         | unionDeclarationStatement
                                         | structDeclarationStatement
                                         | procedureDeclarationStatement
                                         | constantInitializationStatement
                                         | bitsetDeclarationStatement
                                         | variableInitializationStatement
                                         | variableDeclarationStatement
                                         | procedureOverloadStatement
                                         | foreignStatement
                                         | whenStatement
                                         | directive_expression

// @require foreign import entry "entry_unix_no_crt_amd64.asm"
foreignImportDeclarationStatement    ::= (attributeStatement EOS_TOKEN?)* FOREIGN IMPORT
        ( (declaredIdentifier? DQ_STRING_LITERAL) | (declaredIdentifier? blockStart DQ_STRING_LITERAL (COMMA DQ_STRING_LITERAL)* COMMA? blockEnd)) {methods=[alias="declaredIdentifier" paths="DQ_STRING_LITERAL"]}

foreignStatement                     ::= (attributeStatement EOS_TOKEN?)* FOREIGN IDENTIFIER_TOKEN? foreignBlock

foreignBlock                         ::= blockStart foreignStatementList? blockEnd

foreignStatementList                 ::= (sos? foreignBlockStatement eos)+
foreignBlockStatement                ::= variableDeclarationStatement
                                         |foreignProcedureDeclarationStatement
                                         |whenStatement
foreignProcedureDeclarationStatement ::= (attributeStatement eos?)* declaredIdentifier doubleColonOperator PROC string_literal? LPAREN [paramEntries] RPAREN [ARROW returnParameters] TRIPLE_DASH

// Attributes
attributeStatement                   ::= AT IDENTIFIER_TOKEN
                                         | AT arguments
// Struct
structDeclarationStatement           ::= (attributeStatement eos?)* declaredIdentifier doubleColonOperator DISTINCT? structType
structType                           ::= STRUCT [LPAREN polymorphicParameterList RPAREN [eos? whereClause eos?]] <<enterNoBlockMode>> directive* <<exitNoBlockMode>> structBlock
structBlock                          ::= blockStart [structBody] blockEnd
structBody                           ::= fieldDeclarationStatement (COMMA fieldDeclarationStatement)*  [COMMA|EOS_TOKEN]
fieldDeclarationStatement            ::= USING? tagHead? declaredIdentifier (COMMA declaredIdentifier)* COLON typeDefinition_expression [RAW_STRING_LITERAL] { methods=[ getTypeDefinition ]  }

// Enum
enumDeclarationStatement             ::= (attributeStatement eos?)* USING? declaredIdentifier doubleColonOperator DISTINCT? enumType
enumType                             ::= ENUM [IDENTIFIER_TOKEN (DOT IDENTIFIER_TOKEN)*] enumBlock
enumBlock                            ::= blockStart [enumBody] blockEnd
enumBody                             ::= enumValueDeclaration (COMMA enumValueDeclaration)* [EOS_TOKEN|COMMA]
enumValueDeclaration                       ::= declaredIdentifier [EQ expression]
// Union
unionDeclarationStatement            ::= (attributeStatement eos?)* declaredIdentifier doubleColonOperator DISTINCT? unionType
unionType                            ::= UNION [LPAREN polymorphicParameterList RPAREN [eos? whereClause eos?]] <<enterNoBlockMode>> directive* <<exitNoBlockMode>> unionBlock
unionBlock                           ::= blockStart [unionBody] blockEnd
unionBody                            ::= typeDefinition_expression (COMMA typeDefinition_expression)* [COMMA|EOS_TOKEN]

// Bit Set
bitsetDeclarationStatement           ::= declaredIdentifier doubleColonOperator DISTINCT? bitSetType

// Procedure
procedureDeclarationStatement        ::= (attributeStatement eos?)* declaredIdentifier doubleColonOperator procedureType procedureBody {implements="com.lasagnerd.odin.lang.psi.OdinScopeBlock" methods = [ getBlockStatements getDeclarationsSpecs ] }
procedureType                        ::= directive? PROC string_literal? LPAREN [paramEntries] RPAREN [ARROW returnParameters] <<enterNoBlockMode>> [eos? whereClause eos?] <<exitNoBlockMode>>

whereClause                          ::= WHERE expression (COMMA expression)*
procedureBody                        ::=  tripleDashBlock
                                          | block
private tripleDashBlock              ::= [directive] TRIPLE_DASH {pin=2}
returnParameters                     ::= NOT | LPAREN paramEntries RPAREN | typeDefinition_expression

returnStatement                      ::= [directive] RETURN returnArgumentList?
private returnArgumentList           ::= expression (COMMA expression)* COMMA?

usingStatement                       ::= USING expression

// Procedure overloading
procedureOverloadStatement           ::= (attributeStatement eos?)* declaredIdentifier doubleColonOperator PROC LBRACE identifier (COMMA identifier)* COMMA? RBRACE

// TYPES

// Variables and constants
typeDefinition_expression            ::= DISTINCT? [directive] main {methods=[getMainTypeExpression]}
main                                 ::= type_expression | parenthesized_expression


type_expression                      ::= mapType
                                         | matrixType
                                         | bitSetType
                                         | multiPointerType
                                         | arrayType
                                         | procedureType
                                         | structType
                                         | enumType
                                         | unionType
                                         | pointerType
                                         | qualifiedType
                                         | polymorphicType
                                         | constrainedType

constrainedType                      ::= type_expression DIV type_expression {pin=1}
polymorphicType                      ::= DOLLAR identifier
qualifiedType                        ::= qualifiedNameTypeIdentifier [LPAREN expressionsList RPAREN] {extends=type_expression methods=[getPackageIdentifier getTypeIdentifier]}
private qualifiedNameTypeIdentifier  ::= identifier [DOT type_expression]


pointerType                          ::= CARET type_expression
multiPointerType                     ::= LBRACKET CARET RBRACKET type_expression


bitSetType                           ::= BIT_SET LBRACKET expression [SEMICOLON typeDefinition_expression] RBRACKET
mapType                              ::= MAP LBRACKET typeDefinition_expression RBRACKET typeDefinition_expression {methods=[getKeyType getValueType]}
matrixType                           ::= MATRIX LBRACKET expression COMMA expression RBRACKET typeDefinition_expression
arrayType                            ::= [tagHead] LBRACKET [QUESTION|DYNAMIC|expression] RBRACKET typeDefinition_expression {methods=[getTypeDefinition]}

constantInitializationStatement      ::= symbolDefinitionHead colonClosing expressionsList { methods=[getTypeDefinition]}
colonClosing                         ::= COLON

private symbolDefinitionHead         ::= (attributeStatement eos?)* [tagHead] [USING] identifierList colonOpening typeDefinition_expression?
colonOpening                         ::= COLON

variableInitializationStatement      ::= symbolDefinitionHead EQ expressionsList { methods=[getTypeDefinition]}

identifierList                       ::= declaredIdentifier (COMMA declaredIdentifier)*
declaredIdentifier                   ::= [DOLLAR] IDENTIFIER_TOKEN { mixin = 'com.lasagnerd.odin.lang.psi.OdinIdentifierOwner' implements='com.intellij.psi.PsiNameIdentifierOwner' }

assignmentStatement                  ::= [tagHead] expressionsList assignmentOperator expressionsList
expressionsList                      ::= expression (COMMA expression)*

assignmentOperator                   ::= EQ
                                         | PLUS_EQ
                                         | MINUS_EQ
                                         | STAR_EQ
                                         | DIV_EQ
                                         | MOD_EQ
                                         | REMAINDER_EQ
                                         | AND_EQ
                                         | OR_EQ
                                         | XOR_EQ
                                         | ANDAND_EQ
                                         | OROR_EQ
                                         | LSHIFT_EQ
                                         | RSHIFT_EQ
                                         | BITWISE_AND_NOT_EQ

variableDeclarationStatement         ::= (attributeStatement eos?)* [USING] identifierList COLON typeDefinition_expression { methods=[getTypeDefinition]}

// Parameters for procedures and assignments
private polymorphicParameterList             ::= polymorphicParameter (COMMA polymorphicParameter)* COMMA?
polymorphicParameter                 ::= directive? (
                                         variadicParameterDeclaration
                                         | parameterInitialization
                                         | parameterDecl
                                         )

paramEntries                        ::= paramEntry (COMMA paramEntry)* COMMA?
paramEntry                            ::= directive? (
                                         variadicParameterDeclaration
                                             | parameterInitialization
                                             | parameterDecl
                                             | unnamedParameter
                                         )
fake parameterDeclaration ::= parameter+ typeDefinitionContainer? expression { methods=[getTypeDefinition]}
typeDefinitionContainer ::= typeDefinition_expression

parameter ::=  [USING] directive? declaredIdentifier
parameterDecl                        ::= parameter (COMMA parameter)* COLON typeDefinitionContainer {extends=parameterDeclaration  methods=[getTypeDefinition]}
parameterInitialization              ::= parameter COLON [typeDefinitionContainer] EQ expression {extends=parameterDeclaration methods=[getTypeDefinition] }

variadicParameterDeclaration         ::= parameter COLON RANGE typeDefinitionContainer {extends=parameterDeclaration methods=[getTypeDefinition]}
unnamedParameter                     ::= RANGE? typeDefinitionContainer {extends=parameterDeclaration methods=[getTypeDefinition]}

// Function calls
argumentList                         ::= argument (COMMA argument)* COMMA?
namedArgument                        ::= IDENTIFIER_TOKEN EQ expression { extends = argument}
argument                             ::= namedArgument
                                         | unnamedArgument
unnamedArgument                      ::= expression { extends = argument}


// Does it make sense to have literal expressions in the chain?

// All expressions
expression                           ::= postfix_group
                                         | or_else_expression
                                         | ternary_group
                                         | range_group
                                         | or_group
                                         | and_group
                                         | order_group
                                         | add_group
                                         | mul_group
                                         | unary_group
                                         | implicit_selector_expression
                                         | maybe_expression
                                         | uninitialized_expression
                                         | call_expression
                                         | qualification_expression
                                         | index_expression
                                         | slice_expression
                                         | dereference_expression
                                         | procedure_expression
                                         | transmute_expression
                                         | auto_cast_expression
                                         | cast_expression
                                         | compound_literal_expression
                                         | primary_group



fake binary_expression               ::= expression 'OP' expression {methods=[left="/expression[0]" getOperator right="/expression[1]"]}


mul_expression                       ::= expression STAR            <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
div_expression                       ::= expression DIV             <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
mod_expression                       ::= expression MOD             <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
remainder_expression                 ::= expression REMAINDER       <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
add_expression                       ::= expression PLUS            <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
sub_expression                       ::= expression MINUS           <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
bitwise_and_expression               ::= expression AND             <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
bitwise_or_expression                ::= expression PIPE            <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
bitwise_xor_expression               ::= expression TILDE           <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
bitwise_and_not_expression           ::= expression ANDNOT          <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
and_expression                       ::= expression ANDAND          <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
or_expression                        ::= expression OROR            <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
or_else_expression                   ::= expression OR_ELSE         <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression }
in_expression                        ::= expression IN              <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
not_in_expression                    ::= expression NOT_IN          <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
lt_expression                        ::= expression LT              <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
gt_expression                        ::= expression GT              <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
lte_expression                       ::= expression LTE             <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
gte_expression                       ::= expression GTE             <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
eqeq_expression                      ::= expression EQEQ            <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
neq_expression                       ::= expression NEQ             <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
lshift_expression                    ::= expression LSHIFT          <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
rshift_expression                    ::= expression RSHIFT          <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
range_inclusive_expression           ::= expression RANGE_INCLUSIVE <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}
range_exclusive_expression           ::= expression RANGE_EXCLUSIVE <<enterMode "OPERAND">>  expression <<exitMode "OPERAND" >>   { extends=binary_expression elementType=binary_expression}

private mul_group                    ::= mul_expression
                                         | div_expression
                                         | mod_expression
                                         | remainder_expression
                                         | bitwise_and_expression
                                         | lshift_expression
                                         | rshift_expression
                                         | bitwise_and_not_expression

private add_group                    ::= add_expression
                                         | sub_expression
                                         | bitwise_or_expression
                                         | bitwise_xor_expression
                                         | in_expression
                                         | not_in_expression

private order_group                  ::= lt_expression
                                         | gt_expression
                                         | lte_expression
                                         | gte_expression
                                         | eqeq_expression
                                         | neq_expression

private and_group                    ::= and_expression

private or_group                     ::= or_expression

private range_group                  ::= range_inclusive_expression
                                         | range_exclusive_expression

private ternary_group                ::= elvis_expression
                                         | ternary_if_expression
                                         | ternary_when_expression

private postfix_group                ::= or_return_expression
                                         | or_break_expression
                                         | or_continue_expression
                                         | typeAssertion_expression


parenthesized_expression             ::= LPAREN <<enterMode "PAR">> expression <<exitMode "PAR">> RPAREN

// should we define the arrow operator on its own?
fake ref_expression                  ::= expression? (DOT|ARROW) identifier
simple_ref_expression                ::= identifier {extends=ref_expression elementType=ref_expression}
qualification_expression             ::= expression (DOT|ARROW) identifier {extends=ref_expression elementType=ref_expression}


implicit_selector_expression         ::= DOT identifier
index_expression                     ::= expression index
slice_expression                     ::= expression slice
typeAssertion_expression             ::= expression DOT LPAREN type_expression RPAREN {methods=[subject="/expression[0]" targetType="/expression[1]"]}
procedure_expression                 ::= procedure_expression_type procedureBody {implements="com.lasagnerd.odin.lang.psi.OdinScopeBlock" methods = [ getBlockStatements getDeclarationsSpecs ] }
procedure_expression_type            ::= procedureType
dereference_expression               ::= expression CARET
identifier                           ::= IDENTIFIER_TOKEN { methods = [getReference]}

call_expression                      ::= expression arguments

private primary_group                ::= simple_ref_expression | parenthesized_expression | typeDefinition_expression | directive_expression | literal_expression

literal_expression                   ::= basic_literal

compound_literal_expression          ::= compound_literal_typed | compound_literal_untyped
fake compound_value                  ::= {methods=[getCompoundValueBody]}
fake compound_literal                ::= type_expression? (compound_value_typed | compound_value_untyped)
compound_literal_typed               ::= compoundType compound_value_typed {extends=compound_literal}
compound_literal_untyped             ::= compound_value_untyped {extends=compound_literal}

index                                ::= LBRACKET expression [COMMA expression] RBRACKET
slice                                ::= LBRACKET expression? COLON expression? RBRACKET
private arguments                    ::= LPAREN <<enterMode "PAR">>  argumentList? <<exitMode "PAR">> RPAREN

elvis_expression                     ::= expression QUESTION <<enterMode "OPERAND">> expression COLON expression <<exitMode "OPERAND">>
ternary_if_expression                ::= expression if       <<enterMode "OPERAND">> expression else expression  <<exitMode "OPERAND">>
ternary_when_expression              ::= expression WHEN     <<enterMode "OPERAND">> expression else expression  <<exitMode "OPERAND">>

cast_expression                      ::= CAST LPAREN typeDefinition_expression RPAREN expression {methods=[typeDefinitionExpression="/expression[0]"]}
transmute_expression                 ::= TRANSMUTE LPAREN expression RPAREN expression
auto_cast_expression                 ::= AUTO_CAST expression
nil_literal                          ::= NIL
uninitialized_expression             ::= TRIPLE_DASH

// TODO this is a duplicate of compoundLiteralValue but without the non-consuming <<>>-rules
// --> fix this
compound_value_untyped               ::= ((<<isModeOn "OPERAND">> | <<isModeOn "PAR">> | <<isModeOff "NO_BLOCK">>) compoundValueStart [compoundValueBody] compoundValueEnd)
                                         | (compoundValueStart [compoundValueBody] compoundValueEnd <<beforeOperator>>) {extends=compound_value elementType=compound_value}
maybe_expression                     ::= expression DOT_QUESTION

private unary_group                  ::= unary_plus_expression
                                         | unary_minus_expression
                                         | unary_tilde_expression
                                         | unary_and_expression
                                         | unary_not_expression
                                         | unary_range_expression



unary_plus_expression                ::= PLUS  <<enterMode "OPERAND">> expression <<exitMode "OPERAND">>
unary_minus_expression               ::= MINUS <<enterMode "OPERAND">> expression <<exitMode "OPERAND">>
unary_tilde_expression               ::= TILDE <<enterMode "OPERAND">> expression <<exitMode "OPERAND">>
unary_and_expression                 ::= AND   <<enterMode "OPERAND">> expression <<exitMode "OPERAND">>
unary_not_expression                 ::= NOT   <<enterMode "OPERAND">> expression <<exitMode "OPERAND">>
unary_range_expression               ::= RANGE <<enterMode "OPERAND">> expression <<exitMode "OPERAND">>


or_return_expression                 ::= expression OR_RETURN
or_break_expression                  ::= expression OR_BREAK [IDENTIFIER_TOKEN]
or_continue_expression               ::= expression OR_CONTINUE [IDENTIFIER_TOKEN]


string_literal                       ::= DQ_STRING_LITERAL
                                         | SQ_STRING_LITERAL
                                         | RAW_STRING_LITERAL

numeric_literal                      ::= INTEGER_DEC_LITERAL
                                         | INTEGER_HEX_LITERAL
                                         | INTEGER_OCT_LITERAL
                                         | INTEGER_BIN_LITERAL
                                         | FLOAT_DEC_LITERAL
                                         | COMPLEX_INTEGER_DEC_LITERAL
                                         | COMPLEX_FLOAT_LITERAL

boolean_literal                      ::= TRUE
                                         | FALSE

basic_literal                        ::= nil_literal
                                         | string_literal
                                         | numeric_literal
                                         | boolean_literal
//                                         | tripleDashLiteral_expression

private compoundType                 ::= arrayType | matrixType | bitSetType  | mapType | structType | qualifiedType | parExpressionType
parExpressionType                    ::= parenthesized_expression


compound_value_typed                 ::= ((<<isModeOn "PAR">> | <<isModeOff "NO_BLOCK">>) compoundValueStart [compoundValueBody] compoundValueEnd)
                                         | (compoundValueStart [compoundValueBody] compoundValueEnd <<beforeComma>>) {extends=compound_value elementType=compound_value}
compoundValueStart                   ::= LBRACE
compoundValueEnd                     ::= RBRACE
compoundValueBody                    ::= [expression EQ] expression (COMMA [expression EQ] expression)* [EOS_TOKEN|COMMA]

directive_expression              ::= tagHead [basic_literal | (LPAREN expressionsList? RPAREN)]
directive                         ::= tagHead [basic_literal | (LPAREN expressionsList? RPAREN)]
tagHead                              ::= HASH IDENTIFIER_TOKEN



breakStatement                       ::= BREAK IDENTIFIER_TOKEN?
continueStatement                    ::= CONTINUE IDENTIFIER_TOKEN?

// Statements that are allowed in "blocks" like for loops, if statements, etc.
// A block is a sequence of statements contained in braces.
block                                ::= [eos] directive* [eos] blockStart statementList? blockEnd {methods=[getStatements]}
blockStart                           ::= LBRACE
blockEnd                             ::= RBRACE

// In the current implementation, when entering a parenthesis, bracket or brace block, the parser will expect to find
// a statement or an end of statement. The problem is that empty blocks are allowed, but will lead to a parsing error.

eos                                  ::= SEMICOLON | <<eof>> | EOS_TOKEN | <<multilineBlockComment>> | <<atClosingBrace>> | <<afterClosingBrace>>
sos                                  ::= SEMICOLON | NEW_LINE | EOS_TOKEN | <<afterClosingBrace>> | <<multilineBlockComment>>
statementList                        ::= (sos? statement eos)+
statement                            ::= assignmentStatement
                                         | procedureDeclarationStatement
                                         | variableInitializationStatement
                                         | structDeclarationStatement
                                         | enumDeclarationStatement
                                         | unionDeclarationStatement
                                         | procedureOverloadStatement
                                         | constantInitializationStatement
                                         | variableDeclarationStatement
                                         | bitsetDeclarationStatement
                                         | blockStatement
                                         | forInStatement
                                         | forStatement
                                         | conditionalStatement
                                         | whenStatement
                                         | switchStatement
                                         | deferStatement
                                         | returnStatement
                                         | breakStatement
                                         | continueStatement
                                         | usingStatement
                                         | expressionStatement
                                         | fallthroughStatement
                                         | foreignImportDeclarationStatement
                                         | foreignStatement
                                         | importDeclarationStatement


fallthroughStatement                 ::= FALLTHROUGH
expressionStatement                  ::= or_return_expression
                                         | or_break_expression
                                         | or_continue_expression
                                         | call_expression
                                         | qualification_expression
                                         | primary_group

label                                ::= IDENTIFIER_TOKEN COLON
blockStatement                       ::= [(directive eos?)|label] block {implements="com.lasagnerd.odin.lang.psi.OdinScopeBlock" methods = [ getBlockStatements getDeclarationsSpecs ] }

deferStatement                       ::= DEFER deferrableStatement
private deferrableStatement          ::= assignmentStatement
                                         | expression
                                         | conditionalStatement
                                         | forStatement
                                         | switchStatement
                                         | block

// Control Flows

// Shared rules among all control flow statements
controlFlowInit                      ::= assignmentStatement
                                         | variableInitializationStatement
                                         | variableDeclarationStatement
                                         | call_expression

condition                            ::= expression
doStatement                          ::= DO statement
statementBody                ::= block|doStatement

// For
forStatement                         ::= label? directive? forBlock
private forHead                      ::= <<enterNoBlockMode>> [[[controlFlowInit] SEMICOLON] condition? [SEMICOLON [forUpdate]]] <<exitNoBlockMode>>
forBlock                             ::= FOR forHead statementBody {implements="com.lasagnerd.odin.lang.psi.OdinScopeBlock" methods = [ getBlockStatements getDeclarationsSpecs ] }
forUpdate                            ::= assignmentStatement | call_expression

// For-In
forInStatement                       ::= label? directive? forInBlock
forInBlock                           ::= FOR <<enterNoBlockMode>> [forInExpression (COMMA forInExpression)*] IN expression <<exitNoBlockMode>> statementBody {implements="com.lasagnerd.odin.lang.psi.OdinScopeBlock" methods = [ getBlockStatements getDeclarationsSpecs ] }
forInExpression                      ::= AND? declaredIdentifier

// Conditional statement (=if-elseif-else)
private conditionalHead              ::= <<enterNoBlockMode>> [[controlFlowInit] SEMICOLON] condition <<exitNoBlockMode>>

conditionalStatement                 ::= [label] [tagHead] ifBlock (sos elseIfBlock)* [sos elseBlock]

ifBlock                              ::= if conditionalHead statementBody {
                                                implements="com.lasagnerd.odin.lang.psi.OdinScopeBlock"
                                                methods = [ getBlockStatements getDeclarationsSpecs ]
                                            }
elseIfBlock                          ::= elseIf conditionalHead statementBody {implements="com.lasagnerd.odin.lang.psi.OdinScopeBlock" methods = [ getBlockStatements getDeclarationsSpecs ] }
elseIf                               ::= else if
elseBlock                            ::= else statementBody {implements="com.lasagnerd.odin.lang.psi.OdinScopeBlock" methods = [ getBlockStatements getDeclarationsSpecs ] }
if                                   ::= IF_TOKEN
else                                 ::= ELSE_TOKEN

// Switch
switchStatement                      ::= [label] [directive] SWITCH IN? switchHead switchBody
private switchHead                   ::= <<enterNoBlockMode>> [[controlFlowInit SEMICOLON] [expression]] <<exitNoBlockMode>>
switchBody                           ::= eos? blockStart switchCases blockEnd
switchCases                          ::= switchCase*
switchCase                           ::= CASE [expression (COMMA expression)*] caseBlock
caseBlock                            ::= COLON statementList?

// When statement (compile time, which means it does not support an init block)
whenStatement                        ::= [label] [tagHead] WHEN <<enterNoBlockMode>> condition <<exitNoBlockMode>> statementBody (sos elseWhenBlock)* [sos elseBlock]
elseWhenBlock                        ::= else WHEN <<enterNoBlockMode>>  condition <<exitNoBlockMode>> statementBody



