{
  parserImports="static com.intellij.lang.parser.GeneratedParserUtilBase.*"
  parserClass="com.lasagnerd.odin.lang.OdinParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Odin"
  psiImplClassSuffix="Impl"
  psiPackage="com.lasagnerd.odin.lang.psi"
  psiImplPackage="com.lasagnerd.odin.lang.psi.impl"

  elementTypeHolderClass="com.lasagnerd.odin.lang.psi.OdinTypes"
  elementTypeClass="com.lasagnerd.odin.lang.psi.OdinElementType"
  tokenTypeClass="com.lasagnerd.odin.lang.psi.OdinTokenType"
  parserUtilClass="com.lasagnerd.odin.lang.OdinParserUtil"

  tokens=[
        LINE_COMMENT='regexp://[^\r\n]*'
        BLOCK_COMMENT='regexp:/\*[\s\S]*?\*/'

        PACKAGE='regexp:package'
        IMPORT='regexp:import'
        PROC='regexp:proc'
        RETURN='regexp:return'
        DEFER='regexp:defer'
        STRUCT='regexp:struct'
        FOR='regexp:for'
        IN='regexp:in'
        NOT_IN='regexp:not_in'
        DO='regexp:do'
        IF='regexp:if'
        ELSE='regexp:else'
        SWITCH='regexp:switch'
        CASE='regexp:case'
        FALLTHROUGH='regexp:fallthrough'
        TRUE='regexp:true'
        FALSE='regexp:false'
        WHEN='regexp:when'
        BREAK='regexp:break'
        CONTINUE='regexp:continue'
        NIL='regexp:nil'
        TRIPLE_DASH='regexp:---'
        OR_ELSE='regexp:or_else'
        OR_RETURN='regexp:or_return'
        FOREIGN='regexp:foreign'
        CAST='regexp:cast'
        AUTO_CAST='regexp:AUTO_CAST'
        TRANSMUTE='regexp:TRANSMUTE'
        DYNAMIC='regexp:dynamic'
        BIT_SET='regexp:bit_set'
        MAP='regexp:map'
        USING='regexp:using'
        ENUM='regexp:enum'
        UNION='regexp:union'
        DISTINCT='regexp:distinct'
        WHERE='regexp:where'

        IDENTIFIER='regexp:[a-zA-Z_][a-zA-Z0-9_]*'
        DQ_STRING_LITERAL='regexp:"[^"]*"'
        SQ_STRING_LITERAL="regexp:'[^']+?'"
        RAW_STRING_LITERAL='regexp:`[^`]*`'
        INTEGER_DEC_LITERAL='regexp:[0-9][0-9_]*'
        INTEGER_HEX_LITERAL='regexp:0x[0-9a-fA-F][0-9a-fA-F_]*'
        INTEGER_OCT_LITERAL='regexp:0o[0-7][0-7_]*'
        INTEGER_BIN_LITERAL='regexp:0b[01][01_]*'
        FLOAT_DEC_LITERAL='regexp:[0-9][0-9_]*\.[0-9][0-9_]*'
        COMPLEX_FLOAT_LITERAL='regexp:[0-9][0-9_]*\.[0-9][0-9_]*[ijk]'
        COMPLEX_INTEGER_DEC_LITERAL='regexp:[0-9][0-9_]*[ijk]'

        EQ='regexp:='
        COLON='regexp::'

        LBRACE='regexp:\{'
        RBRACE='regexp:}'
        LPAREN='regexp:\('
        RPAREN='regexp:\)'

        LBRACKET='regexp:\['
        RBRACKET='regexp:]'
        HASH='regexp:#'

        DOT='regexp:\.'
        ARROW='regexp:->'
        COMMA='regexp:,'
        SEMICOLON='regexp:;'
        QUESTION='regexp:\?'
        CARET='regexp:\^'
        AT='regexp:@'
        DOLLAR='regexp:\$'
        DOT_QUESTION='regexp:\.\?'

        // Operators
        EQEQ='regexp:=='
        NEQ='regexp:!='
        LT='regexp:<'
        GT='regexp:>'
        LTE='regexp:<='
        GTE='regexp:>='

        // Boolean operators
        ANDAND='regexp:&&'
        OROR='regexp:\|\|'
        NOT='regexp:!'
        ANDNOT='regexp:&~'


        // Arithmetic operators
        PLUS='regexp:\+'
        MINUS='regexp:-'
        STAR='regexp:\*'
        DIV='regexp:/'
        MOD='regexp:%'
        REMAINDER='regexp:%%'

        // Bitwise operators
        AND='regexp:&'
        PIPE='regexp:\|'
        TILDE='regexp:~'
        LSHIFT='regexp:<<'
        RSHIFT='regexp:>>'


        // Assignment operators
        PLUS_EQ='regexp:\+='
        MINUS_EQ='regexp:-='
        STAR_EQ='regexp:\*='
        DIV_EQ='regexp:/='
        MOD_EQ='regexp:%='
        REMAINDER_EQ='regexp:%%='
        AND_EQ='regexp:&='
        OR_EQ='regexp:\|='
        XOR_EQ='regexp:~='
        ANDAND_EQ='regexp:&&='
        OROR_EQ='regexp:\|\|='
        LSHIFT_EQ='regexp:<<='
        RSHIFT_EQ='regexp:>>='

        // Range Operators
        RANGE = 'regexp:\.\.'
        RANGE_INCLUSIVE='regexp:\.\.='
        RANGE_EXCLUSIVE='regexp:\.\.<'

        WHITE_SPACE='regexp:[ \t\f]+'
        NEW_LINE='regexp:(\r|\n|\r\n)+'
        EOS='EOS'
  ]

  extends('.*_expression') = expression
  extends('.*[sS]tatement') = statement
  extends('.*_primary') = primary
}


odinFile ::= packageClause eosRule (importDeclarationStatement eosRule)* (fileScopeStatement eosRule)* <<eof>>
packageClause ::= PACKAGE IDENTIFIER
importDeclarationStatement ::= IMPORT IDENTIFIER? DQ_STRING_LITERAL

fileScopeStatement ::= foreignImportDeclarationStatement
                | importDeclarationStatement
                | enumDeclarationStatement
                | procedureDeclarationStatement
                | constantInitializationStatement
                | unionDeclarationStatement
                | typeAliasDeclarationStatement
                | structDeclarationStatement
                | bitsetDeclarationStatement
                | variableInitializationStatement
                | variableDeclarationStatement
                | procedureOverloadStatement
                | foreignStatement
                | whenStatement
                | tagStatement_expression


foreignImportDeclarationStatement ::= FOREIGN IMPORT ( IDENTIFIER? DQ_STRING_LITERAL | IDENTIFIER? LBRACE DQ_STRING_LITERAL (COMMA DQ_STRING_LITERAL)* COMMA? RBRACE)

foreignStatement ::= (attributeStatement eosRule)* FOREIGN IDENTIFIER foreignBlock

foreignBlock ::= LBRACE foreignStatementList? RBRACE

foreignStatementList ::= ((SEMICOLON | NEW_LINE | EOS | <<closingBracket>>)? foreignBlockStatement eosRule)+
foreignBlockStatement ::= variableDeclarationStatement
                         |foreignProcedureDeclarationStatement
foreignProcedureDeclarationStatement ::= (attributeStatement eosRule*)* IDENTIFIER COLON COLON PROC string_literal? LPAREN parameterList* RPAREN [ARROW returnType] TRIPLE_DASH

// Attributes
attributeStatement ::=  AT IDENTIFIER
                       | AT arguments
// Struct
structDeclarationStatement ::= attributeStatement? IDENTIFIER COLON COLON structSpec
structSpec ::= STRUCT [LPAREN parameterList RPAREN [WHERE expression]] tagStatement_expression? LBRACE [fieldDeclarationStatement (COMMA fieldDeclarationStatement)* COMMA?] RBRACE
fieldDeclarationStatement ::= USING? IDENTIFIER (COMMA IDENTIFIER)* COLON typeDefinition_expression

// Enum
enumDeclarationStatement ::= attributeStatement? IDENTIFIER COLON COLON enumSpec
enumSpec ::= ENUM [IDENTIFIER (DOT IDENTIFIER)*] LBRACE ([IDENTIFIER EQ] expression COMMA)* RBRACE

// Union
unionDeclarationStatement ::= attributeStatement? IDENTIFIER COLON COLON UNION tagStatement_expression? unionSpec
unionSpec ::= LBRACE typeDefinition_expression (COMMA typeDefinition_expression)* COMMA? RBRACE

// Bit Set
bitsetDeclarationStatement ::= IDENTIFIER COLON COLON BIT_SET LBRACKET expression [SEMICOLON typeDefinition_expression] RBRACKET

// Procedure

// TODO define declaration as literal expression
// This will be elegant since all declaration follow the pattern
//  IDENTIFIER COLON TYPE COLON LITERAL_EXPRESSION
procedureDeclarationStatement ::= (attributeStatement eosRule*)* IDENTIFIER COLON COLON procedureType procedureBody
procedure_expression ::= procedureType procedureBody
procedureType    ::= tagStatement_expression? PROC string_literal? LPAREN parameterList* RPAREN [ARROW returnType] <<enterMode "BLOCK">> [whereClause] <<exitMode "BLOCK">>

whereClause ::= WHERE expression (COMMA expression)*
procedureBody ::= block|TRIPLE_DASH

returnType          ::= NOT | LPAREN parameterList RPAREN | typeDefinition_expression

returnStatement ::= RETURN returnArgumentList?
private returnArgumentList ::= expression (COMMA expression)*

usingStatement ::= USING expression

// Procedure overloading
procedureOverloadStatement ::= IDENTIFIER COLON COLON PROC LBRACE IDENTIFIER (COMMA IDENTIFIER)* COMMA? RBRACE

// Variables and constants
typeDefinition_expression   ::= DISTINCT? type
type ::= mapType
         | matrixType
         | bitSetType
         | multiPointerType
         | arrayType
         | procedureType
         | structSpec
         | enumSpec
         | unionSpec
         | structType
         | pointerType
         | (DOLLAR? primary_expression)

pointerType ::= CARET type
multiPointerType ::= LBRACKET CARET RBRACKET type

structType      ::= IDENTIFIER (DOT IDENTIFIER)* LPAREN argumentList* RPAREN

bitSetType       ::= BIT_SET LBRACKET expression [SEMICOLON primary_expression] RBRACKET
mapType          ::= MAP LBRACKET typeDefinition_expression RBRACKET typeDefinition_expression
matrixType       ::= MATRIX LBRACKET expression COMMA expression RBRACKET typeDefinition_expression
arrayType        ::= LBRACKET [QUESTION|DYNAMIC|expression] RBRACKET typeDefinition_expression

constantInitializationStatement ::= (attributeStatement eosRule*)* IDENTIFIER COLON typeDefinition_expression? COLON tagStatement_expression? expression
typeAliasDeclarationStatement ::= IDENTIFIER COLON COLON DISTINCT? typeDefinition_expression

variableInitializationStatement ::= (attributeStatement eosRule*)* identifierList COLON typeDefinition_expression? EQ expressionsList
identifierList ::= IDENTIFIER (COMMA IDENTIFIER)*

assignmentStatement ::=  leftHandExpressions assignmentOperator expressionsList

leftHandExpressions ::= leftHandExpression (COMMA leftHandExpression)*
leftHandExpression ::= matrixIndex_expression
    | arraySlice_expression
    | arrayIndex_expression
    | dereference_expression
    | primary_expression
    | identifier_expression

expressionsList ::= expression (COMMA expression)*

assignmentOperator ::= EQ
                       | PLUS_EQ
                       | MINUS_EQ
                       | STAR_EQ
                       | DIV_EQ
                       | MOD_EQ
                       | REMAINDER_EQ
                       | AND_EQ
                       | OR_EQ
                       | XOR_EQ
                       | ANDAND_EQ
                       | OROR_EQ
                       | LSHIFT_EQ
                       | RSHIFT_EQ

variableDeclarationStatement ::= (attributeStatement eosRule?)* IDENTIFIER (COMMA IDENTIFIER)* COLON typeDefinition_expression

// Parameters for procedures and assignments
parameterList ::= parameter (COMMA parameter)*
parameter ::= tagStatement_expression? (
              variadicParameter
              | parameterInitialization
              | parameterDeclarationStatement
              | typeDefinition_expression
              )

parameterDeclarationStatement ::= [USING|DOLLAR] tagStatement_expression? IDENTIFIER (COMMA DOLLAR? IDENTIFIER)* COLON expression parameterTypeSpecialization?
parameterTypeSpecialization ::= DIV expression
private parameterInitialization ::= IDENTIFIER COLON typeDefinition_expression? EQ expression
private variadicParameter ::= IDENTIFIER COLON RANGE expression


// Function calls
argumentList  ::= argument (COMMA argument)*
namedArgument ::= IDENTIFIER EQ expression { extends = argument}
argument      ::= namedArgument
                  | unnamedArgument
unnamedArgument ::= expression { extends = argument}

call_expression ::= caller LPAREN argumentList? RPAREN

caller ::=  parenthesized_expression | identifier_expression


binary_expression ::= expression binaryOperator expression
binaryOperator ::= PLUS
                   | MINUS
                   | STAR
                   | DIV
                   | MOD
                   | REMAINDER
                   | PIPE
                   | TILDE
                   | ANDAND
                   | ANDNOT
                   | AND
                   | OROR
                   | LSHIFT
                   | RSHIFT
                   | EQEQ
                   | NEQ
                   | LT
                   | GT
                   | LTE
                   | GTE
                   | RANGE_INCLUSIVE
                   | RANGE_EXCLUSIVE
                   | OR_ELSE
                   | IN
                   | NOT_IN

parenthesized_expression ::= LPAREN <<enterMode "PAR">> expression <<exitMode "PAR">> RPAREN

// Does it make sense to have literal expressions in the chain?

// All expressions
expression ::= implicitSelector_expression
               | primary_expression
               | identifier_expression
               | ternary_expression
               | procedure_expression
               | dereference_expression
               | maybe_expression
               | unary_expression
               | binary_expression
               | or_return_expression
               | set_expression
               | uninitialized_expression
               | cast_expression
               | transmute_expression
               | auto_cast_expression
               | typeDefinition_expression
               | tagStatement_expression

primary_expression ::= primary
primary ::= operand_primary | caret_primary | combined_primary
combined_primary ::= primary ( DOT IDENTIFIER
                              | index
                              | typeAssertion
                              | slice
                              | arguments
                              )
caret_primary ::= primary CARET
operand_primary ::=  literal | identifier_expression | parenthesized_expression

private index ::= LBRACKET expression [COMMA expression] RBRACKET
private slice ::= LBRACKET expression? COLON expression? RBRACKET
private arguments ::= LPAREN argumentList? RPAREN
private typeAssertion ::= DOT LPAREN typeDefinition_expression RPAREN

private ternary_expression ::= ternary_cond_expression
                               | ternary_if_expression
                               | ternary_when_expression
ternary_cond_expression ::= expression QUESTION expression COLON expression
ternary_if_expression ::= expression IF expression ELSE expression
ternary_when_expression ::= expression WHEN expression ELSE expression

cast_expression ::= CAST LPAREN typeDefinition_expression RPAREN expression
transmute_expression ::= TRANSMUTE LPAREN expression RPAREN expression
auto_cast_expression ::= AUTO_CAST expression
nil_literal ::= NIL
uninitialized_expression ::= TRIPLE_DASH
identifier_expression ::= IDENTIFIER
set_expression ::= LBRACE [expression (COMMA expression)* COMMA?] RBRACE

implicitSelector_expression ::= DOT expression
dereference_expression ::= expression CARET
maybe_expression ::= expression DOT_QUESTION

unaryOperator ::= PLUS      // Arithmetic identity
                  | MINUS   // Arithmetic negation
                  | NOT     // Boolean not
                  | TILDE   // Bitwise not
                  | AND     // Address of
                  | RANGE
unary_expression ::= unaryOperator primary_expression
or_return_expression ::= expression OR_RETURN


string_literal  ::= DQ_STRING_LITERAL
                    | SQ_STRING_LITERAL
                    | RAW_STRING_LITERAL
numeric_literal ::= INTEGER_DEC_LITERAL
                    | INTEGER_HEX_LITERAL
                    | INTEGER_OCT_LITERAL
                    | INTEGER_BIN_LITERAL
                    | FLOAT_DEC_LITERAL
                    | COMPLEX_INTEGER_DEC_LITERAL
                    | COMPLEX_FLOAT_LITERAL

boolean_literal ::= TRUE
                    | FALSE


basic_literal ::= nil_literal
                  | string_literal
                  | numeric_literal
                  | boolean_literal
                  | tripleDashLiteral_expression

composite_literal ::= mapLiteral
                      | matrixLiteral
                      | arrayLiteral
                      | sliceLiteral
                      | structLiteral


literal ::= basic_literal | composite_literal

intLiteral ::= INTEGER_DEC_LITERAL
              | INTEGER_HEX_LITERAL
              | INTEGER_OCT_LITERAL
              | INTEGER_BIN_LITERAL
              | SQ_STRING_LITERAL
              | DOT IDENTIFIER


structLiteral ::= IDENTIFIER (DOT IDENTIFIER)* literalValue
literalValue ::= (<<isModeOn "PAR">> | <<isModeOff "BLOCK">>)  LBRACE [[IDENTIFIER EQ] expression (COMMA [IDENTIFIER EQ] expression)* COMMA?] RBRACE
arrayLiteral ::= LBRACKET (expression|QUESTION) RBRACKET typeDefinition_expression LBRACE [arrayIndexedList|arrayExpressionsList] RBRACE
private arrayExpressionsList ::=expression (COMMA expression)* COMMA?
private arrayIndexedList ::= arrayIndexedInitialization (COMMA arrayIndexedInitialization)* COMMA?
private arrayIndexedInitialization ::= ((intLiteral (RANGE_INCLUSIVE|RANGE_EXCLUSIVE) intLiteral)|intLiteral) EQ expression

sliceLiteral ::= LBRACKET RBRACKET expression LBRACE [expression (COMMA expression)* COMMA?] RBRACE
matrixLiteral ::= MATRIX LBRACKET INTEGER_DEC_LITERAL COMMA INTEGER_DEC_LITERAL RBRACKET LBRACE expression (COMMA expression)* RBRACE
mapLiteral ::= mapType LBRACE [mapInitAssignments] RBRACE
mapInitAssignments ::= mapInitAssigment (COMMA mapInitAssigment)* COMMA?
mapInitAssigment ::= expression EQ expression
tripleDashLiteral_expression ::= TRIPLE_DASH
// indexing operator can be applied to same expressions that can be called?
arrayIndex_expression ::= operand_primary LBRACKET expression RBRACKET
arraySlice_expression ::= operand_primary LBRACKET expression? COLON expression? RBRACKET
matrixIndex_expression ::= operand_primary LBRACKET expression COMMA expression RBRACKET

tagStatement_expression ::= tagHead [literal | (LPAREN expressionsList? RPAREN)]
tagHead ::= HASH IDENTIFIER

forStatement ::= label? tagStatement_expression? forHead forBody
private label ::= IDENTIFIER COLON


forHead ::= FOR <<enterMode "BLOCK">> (forIn|forTraditional) <<exitMode "BLOCK">>
forBody ::= block | forSingleStatement
forSingleStatement ::= DO statement

breakStatement ::= BREAK IDENTIFIER?
continueStatement ::= CONTINUE IDENTIFIER?

// Statements that are allowed in "blocks" like for loops, if statements, etc.
// A block is a sequence of statements contained in braces.
block ::= tagStatement_expression? LBRACE statementList? RBRACE


// In the current implementation, when entering a parenthesis, bracket or brace block, the parser will expect to find
// a statement or an end of statement. The problem is that empty blocks are allowed, but will lead to a parsing error.

// TODO introduce new lexer state that ignores EOS tokens

eosRule ::= SEMICOLON | <<eof>> | EOS | <<closingBracket>>
statementList ::= ((SEMICOLON | NEW_LINE | EOS | <<closingBracket>>)? statement eosRule)+
statement ::= assignmentStatement
                | procedureDeclarationStatement
                | variableInitializationStatement
                | variableDeclarationStatement
                | enumDeclarationStatement
                | procedureOverloadStatement
                | constantInitializationStatement
                | bitsetDeclarationStatement
                | blockStatement
                | forStatement
                | ifStatement
                | whenStatement
                | switchStatement
                | deferStatement
                | returnStatement
                | breakStatement
                | continueStatement
                | attributeStatement
                | usingStatement
                | expressionStatement
                | fallthroughStatement
                | foreignStatement
                | foreignImportDeclarationStatement

fallthroughStatement ::= FALLTHROUGH
expressionStatement ::= expression
blockStatement ::= [(tagStatement_expression eosRule*)|label] block

private forTraditional ::= (forStartStatement? SEMICOLON)? expression? (SEMICOLON forEndStatement?)?
private forIn ::=  IDENTIFIER (COMMA IDENTIFIER)* IN expression

private forStartStatement ::=   variableInitializationStatement | assignmentStatement | call_expression
private forEndStatement ::=   assignmentStatement | call_expression

ifStatement ::= IF condition block (eosRule* elseIfBlock)* (eosRule* elseBlock)?
private condition ::= <<enterMode "BLOCK">> [ifInit SEMICOLON] expression <<exitMode "BLOCK">>
private switchCondition ::= [ifInit SEMICOLON] expression?
private ifInit ::= assignmentStatement|variableInitializationStatement
elseIfBlock ::= ELSE IF condition block
elseBlock ::= ELSE block

whenStatement ::= WHEN condition block (eosRule* elseWhenBlock)* (eosRule* elseBlock)?
elseWhenBlock ::= ELSE WHEN expression block

switchStatement ::= tagStatement_expression? SWITCH IN? <<enterMode "BLOCK">> switchCondition? <<exitMode "BLOCK">> LBRACE (switchCaseBlock)* RBRACE
switchCaseBlock ::= CASE (caseExpression(COMMA caseExpression)*)? COLON statementList?

caseExpression ::= expression
                  | typeDefinition_expression
                  | DOT IDENTIFIER

deferStatement ::= DEFER deferrableStatement
private deferrableStatement ::= assignmentStatement
                                | expression
                                | ifStatement
                                | forStatement
                                | switchStatement
                                | block
