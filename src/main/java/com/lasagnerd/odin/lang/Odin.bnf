{
  parserImports="static com.intellij.lang.parser.GeneratedParserUtilBase.*"
  parserClass="com.lasagnerd.odin.lang.OdinParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Odin"
  psiImplClassSuffix="Impl"
  psiPackage="com.lasagnerd.odin.lang.psi"
  psiImplPackage="com.lasagnerd.odin.lang.psi.impl"

  elementTypeHolderClass="com.lasagnerd.odin.lang.psi.OdinTypes"
  elementTypeClass="com.lasagnerd.odin.lang.psi.OdinElementType"
  tokenTypeClass="com.lasagnerd.odin.lang.psi.OdinTokenType"
  parserUtilClass="com.lasagnerd.odin.lang.OdinParserUtil"

  tokens=[
        LINE_COMMENT='regexp://[^\r\n]*'
        BLOCK_COMMENT='regexp:/\*[\s\S]*?\*/'

        PACKAGE='regexp:package'
        IMPORT='regexp:import'
        PROC='regexp:proc'
        RETURN='regexp:return'
        DEFER='regexp:defer'
        STRUCT='regexp:struct'
        FOR='regexp:for'
        IN='regexp:in'
        NOT_IN='regexp:not_in'
        DO='regexp:do'
        IF='regexp:if'
        ELSE='regexp:else'
        SWITCH='regexp:switch'
        CASE='regexp:case'
        FALLTHROUGH='regexp:fallthrough'
        TRUE='regexp:true'
        FALSE='regexp:false'
        WHEN='regexp:when'
        BREAK='regexp:break'
        CONTINUE='regexp:continue'
        NIL='regexp:nil'
        TRIPLE_DASH='regexp:---'
        OR_ELSE='regexp:or_else'
        OR_RETURN='regexp:or_return'
        FOREIGN='regexp:foreign'
        CAST='regexp:cast'
        AUTO_CAST='regexp:AUTO_CAST'
        TRANSMUTE='regexp:TRANSMUTE'
        DYNAMIC='regexp:dynamic'
        BIT_SET='regexp:bit_set'
        MAP='regexp:map'
        USING='regexp:using'
        ENUM='regexp:enum'
        UNION='regexp:union'
        DISTINCT='regexp:distinct'
        WHERE='regexp:where'

        IDENTIFIER='regexp:[a-zA-Z_][a-zA-Z0-9_]*'
        DQ_STRING_LITERAL='regexp:"[^"]*"'
        SQ_STRING_LITERAL="regexp:'[^']+?'"
        RAW_STRING_LITERAL='regexp:`[^`]*`'
        INTEGER_DEC_LITERAL='regexp:[0-9][0-9_]*'
        INTEGER_HEX_LITERAL='regexp:0x[0-9a-fA-F][0-9a-fA-F_]*'
        INTEGER_OCT_LITERAL='regexp:0o[0-7][0-7_]*'
        INTEGER_BIN_LITERAL='regexp:0b[01][01_]*'
        FLOAT_DEC_LITERAL='regexp:[0-9][0-9_]*\.[0-9][0-9_]*'
        COMPLEX_FLOAT_LITERAL='regexp:[0-9][0-9_]*\.[0-9][0-9_]*[ijk]'
        COMPLEX_INTEGER_DEC_LITERAL='regexp:[0-9][0-9_]*[ijk]'

        EQ='regexp:='
        COLON='regexp::'

        LBRACE='regexp:\{'
        RBRACE='regexp:}'
        LPAREN='regexp:\('
        RPAREN='regexp:\)'

        LBRACKET='regexp:\['
        RBRACKET='regexp:]'
        HASH='regexp:#'

        DOT='regexp:\.'
        ARROW='regexp:->'
        COMMA='regexp:,'
        SEMICOLON='regexp:;'
        QUESTION='regexp:\?'
        CARET='regexp:\^'
        AT='regexp:@'
        DOLLAR='regexp:\$'

        // Operators
        EQEQ='regexp:=='
        NEQ='regexp:!='
        LT='regexp:<'
        GT='regexp:>'
        LTE='regexp:<='
        GTE='regexp:>='

        // Boolean operators
        ANDAND='regexp:&&'
        OROR='regexp:\|\|'
        NOT='regexp:!'
        ANDNOT='regexp:&~'


        // Arithmetic operators
        PLUS='regexp:\+'
        MINUS='regexp:-'
        STAR='regexp:\*'
        DIV='regexp:/'
        MOD='regexp:%'
        REMAINDER='regexp:%%'

        // Bitwise operators
        AND='regexp:&'
        PIPE='regexp:\|'
        TILDE='regexp:~'
        LSHIFT='regexp:<<'
        RSHIFT='regexp:>>'


        // Assignment operators
        PLUS_EQ='regexp:\+='
        MINUS_EQ='regexp:-='
        STAR_EQ='regexp:\*='
        DIV_EQ='regexp:/='
        MOD_EQ='regexp:%='
        REMAINDER_EQ='regexp:%%='
        AND_EQ='regexp:&='
        OR_EQ='regexp:\|='
        XOR_EQ='regexp:~='
        ANDAND_EQ='regexp:&&='
        OROR_EQ='regexp:\|\|='
        LSHIFT_EQ='regexp:<<='
        RSHIFT_EQ='regexp:>>='

        // Range Operators
        RANGE = 'regexp:\.\.'
        RANGE_INCLUSIVE='regexp:\.\.='
        RANGE_EXCLUSIVE='regexp:\.\.<'

        WHITE_SPACE='regexp:\s+|\r?\n'
  ]

  extends('.*_expression') = expression
  extends('.*[sS]tatement') = statement
}


odinFile ::= packageDeclarationStatement (fileScopeStatement)*
packageDeclarationStatement ::= PACKAGE IDENTIFIER
importDeclarationStatement ::= IMPORT IDENTIFIER? DQ_STRING_LITERAL

private fileScopeStatement ::= singleFileScopeStatement (SEMICOLON singleFileScopeStatement)*

// TODO We have to distinguish between statements that have to be alone in a line or can be chained using a semicolon

private singleFileScopeStatement ::= foreignImportDeclarationStatement
                | importDeclarationStatement
                | procedureDeclarationStatement
                | constantInitialization
                | enumDeclarationStatement
                | unionDeclarationStatement
                | typeAliasDeclarationStatement
                | structDeclarationStatement
                | bitsetDeclarationStatement
                | variableInitialization
                | variableDeclarationStatement
                | procedureOverloadStatement
                | foreignStatement
                | tagStatement_expression


foreignImportDeclarationStatement ::= FOREIGN IMPORT ( IDENTIFIER? DQ_STRING_LITERAL | LBRACE DQ_STRING_LITERAL (COMMA DQ_STRING_LITERAL)* RBRACE)

foreignStatement ::= attributeStatement? FOREIGN IDENTIFIER foreignBlock
foreignBlock ::= LBRACE (foreignProcedureDeclarationStatement|variableDeclarationStatement) RBRACE
foreignProcedureDeclarationStatement ::= attributeStatement? IDENTIFIER COLON COLON PROC string_literal? LPAREN parameterList* RPAREN (ARROW returnType)? TRIPLE_DASH

// Attributes
attributeStatement ::=  AT IDENTIFIER
                       | AT LPAREN IDENTIFIER (EQ (literal_expression | IDENTIFIER))? RPAREN
// Struct
structDeclarationStatement ::= attributeStatement? IDENTIFIER COLON COLON structSpec
structSpec ::= STRUCT [LPAREN parameterList RPAREN [WHERE expression]] tagStatement_expression? LBRACE (fieldDeclarationStatement COMMA)* RBRACE
fieldDeclarationStatement ::= USING? IDENTIFIER (COMMA IDENTIFIER)* COLON typeDefinition_expression

// Enum
enumDeclarationStatement ::= attributeStatement? IDENTIFIER COLON COLON enumSpec
enumSpec ::= ENUM typeDefinition_expression? LBRACE (IDENTIFIER EQ expression COMMA)* RBRACE

// Union
unionDeclarationStatement ::= attributeStatement? IDENTIFIER COLON COLON UNION tagStatement_expression? unionSpec
unionSpec ::= LBRACE typeDefinition_expression (COMMA typeDefinition_expression)* RBRACE

// Bit Set
bitsetDeclarationStatement ::= IDENTIFIER COLON COLON BIT_SET LBRACKET expression [SEMICOLON typeDefinition_expression] RBRACKET

// Procedure
procedureDeclarationStatement ::= attributeStatement? IDENTIFIER COLON COLON procedureType (block|TRIPLE_DASH)

returnType          ::= LPAREN parameterList RPAREN | expression

returnStatement ::= RETURN returnArgumentList?
private returnArgumentList ::= expression (COMMA expression)*

usingStatement ::= USING expression

// Procedure overloading
procedureOverloadStatement ::= IDENTIFIER COLON COLON PROC LBRACE IDENTIFIER (COMMA IDENTIFIER)* COMMA? RBRACE

// Variables and constants
typeDefinition_expression   ::= CARET? (IDENTIFIER DOT)* typeType
private typeType ::= mapType
                     | matrixType
                     | bitSetType
                     | arrayType
                     | procedureType
                     | structSpec
                     | enumSpec
                     | unionSpec
                     | structType
                     | (DOLLAR? identifier_expression)

structType      ::= IDENTIFIER (DOT IDENTIFIER)* LPAREN argumentList* RPAREN

bitSetType       ::= BIT_SET LBRACKET expression [SEMICOLON identifier_expression] RBRACKET
mapType          ::= MAP LBRACKET typeDefinition_expression RBRACKET typeDefinition_expression
matrixType       ::= MATRIX LBRACKET expression COMMA expression RBRACKET typeDefinition_expression
arrayType        ::= LBRACKET [QUESTION|DYNAMIC|expression] RBRACKET typeDefinition_expression
procedureType    ::= tagStatement_expression? PROC string_literal? LPAREN parameterList* RPAREN [ARROW returnType]

constantInitialization ::= attributeStatement? IDENTIFIER COLON typeDefinition_expression? COLON (expression | tagStatement_expression)
typeAliasDeclarationStatement ::= IDENTIFIER COLON COLON DISTINCT? typeDefinition_expression

variableInitialization ::= attributeStatement? identifierList COLON typeDefinition_expression? EQ expressionsList
identifierList ::= IDENTIFIER (COMMA IDENTIFIER)*

assignmentStatement ::=  leftHandExpressions assignmentOperator expressionsList

leftHandExpressions ::= leftHandExpression (COMMA leftHandExpression)*
leftHandExpression ::= matrixIndex_expression
    | arraySlice_expression
    | arrayIndex_expression
    | dereference_expression
    | reference_expression
    | identifier_expression

expressionsList ::= expression (COMMA expression)*

assignmentOperator ::= EQ
                       | PLUS_EQ
                       | MINUS_EQ
                       | STAR_EQ
                       | DIV_EQ
                       | MOD_EQ
                       | REMAINDER_EQ
                       | AND_EQ
                       | OR_EQ
                       | XOR_EQ
                       | ANDAND_EQ
                       | OROR_EQ
                       | LSHIFT_EQ
                       | RSHIFT_EQ

variableDeclarationStatement ::= attributeStatement? IDENTIFIER (COMMA IDENTIFIER)* COLON typeDefinition_expression

// Parameters for procedures and assignments
parameterList ::= parameter (COMMA parameter)*
parameter ::= parameterInitialization
              | parameterDeclarationStatement
              | typeDefinition_expression
              | variadicParameter

parameterDeclarationStatement ::= [USING|DOLLAR] tagStatement_expression? IDENTIFIER (COMMA DOLLAR? IDENTIFIER)* COLON expression parameterTypeSpecialization?
parameterTypeSpecialization ::= DIV expression
private parameterInitialization ::= IDENTIFIER COLON typeDefinition_expression? EQ expression
private variadicParameter ::= IDENTIFIER COLON RANGE expression


// Function calls
argumentList  ::= argument (COMMA argument)*
namedArgument ::= IDENTIFIER EQ expression { extends = argument}
argument      ::= namedArgument
                  | unnamedArgument
unnamedArgument ::= expression { extends = argument}

call_expression ::= caller LPAREN argumentList? RPAREN
caller ::=  parenthesized_expression
            | identifier_expression


binary_expression ::= expression binaryOperator expression
binaryOperator ::= PLUS
                   | MINUS
                   | STAR
                   | DIV
                   | MOD
                   | REMAINDER
                   | PIPE
                   | TILDE
                   | ANDAND
                   | ANDNOT
                   | AND
                   | OROR
                   | LSHIFT
                   | RSHIFT
                   | EQEQ
                   | NEQ
                   | LT
                   | GT
                   | LTE
                   | GTE
                   | RANGE
                   | RANGE_INCLUSIVE
                   | RANGE_EXCLUSIVE
                   | OR_ELSE
                   | IN
                   | NOT_IN

parenthesized_expression ::= LPAREN expression RPAREN

// Does it make sense to have literal expressions in the chain?

// All expressions
expression ::= implicitSelector_expression
               | literal_expression
               | reference_expression
               | arraySlice_expression
               | matrixIndex_expression
               | arrayIndex_expression
               | procedure_expression
               | call_expression
               | dereference_expression
               | parenthesized_expression
               | binary_expression
               | unary_expression
               | or_return_expression
               | set_expression
               | uninitialized_expression
               | cast_expression
               | transmute_expression
               | auto_cast_expression
               | nil_expression
               | identifier_expression
               | typeDefinition_expression
               | tagStatement_expression


//private ternary_expression ::= ternary_cond_expression
//                               | ternary_if_expression
//                               | ternary_when_expression
//ternary_cond_expression ::= expression QUESTION expression COLON expression
//ternary_if_expression ::= expression IF expression ELSE expression
//ternary_when_expression ::= expression WHEN expression ELSE expression

cast_expression ::= CAST LPAREN typeDefinition_expression RPAREN expression
transmute_expression ::= TRANSMUTE LPAREN expression RPAREN expression
auto_cast_expression ::= AUTO_CAST expression
nil_expression ::= NIL
uninitialized_expression ::= TRIPLE_DASH
identifier_expression ::= IDENTIFIER
set_expression ::= LBRACE [expression (COMMA expression)* COMMA?] RBRACE
reference_expression ::= referable_expression (DOT referable_expression)*

private referable_expression ::=
    call_expression
    | parenthesized_expression
    | structLiteral_expression
    | arraySlice_expression
    | arrayIndex_expression
    | matrixIndex_expression
    | unary_expression
    | identifier_expression

implicitSelector_expression ::= DOT expression
dereference_expression ::= expression
                        CARET { pin=2 }

procedure_expression ::= PROC LPAREN parameterList* RPAREN (ARROW returnType)? block

unaryOperator ::= PLUS      // Arithmetic identity
                  | MINUS   // Arithmetic negation
                  | NOT     // Boolean not
                  | TILDE   // Bitwise not
                  | AND     // Address of
unary_expression ::= unaryOperator expression
or_return_expression ::= expression OR_RETURN


string_literal  ::= DQ_STRING_LITERAL
                    | SQ_STRING_LITERAL
                    | RAW_STRING_LITERAL
numeric_literal ::= INTEGER_DEC_LITERAL
                    | INTEGER_HEX_LITERAL
                    | INTEGER_OCT_LITERAL
                    | INTEGER_BIN_LITERAL
                    | FLOAT_DEC_LITERAL
                    | COMPLEX_INTEGER_DEC_LITERAL
                    | COMPLEX_FLOAT_LITERAL

boolean_literal ::= TRUE
                    | FALSE

literal_expression ::= string_literal
         | numeric_literal
         | boolean_literal
         | matrixLiteral_expression
         | arrayLiteral_expression
         | sliceLiteral_expression
         | structLiteral_expression
         | TRIPLE_DASH

structLiteral_expression ::= IDENTIFIER LBRACE (IDENTIFIER EQ expression COMMA)* RBRACE
arrayLiteral_expression ::= LBRACKET (expression|QUESTION) RBRACKET typeDefinition_expression LBRACE [expression (COMMA expression)* COMMA?] RBRACE
sliceLiteral_expression ::= LBRACKET RBRACKET expression LBRACE [expression (COMMA expression)*] RBRACE
matrixLiteral_expression ::= MATRIX LBRACKET INTEGER_DEC_LITERAL COMMA INTEGER_DEC_LITERAL RBRACKET LBRACE expression (COMMA expression)* RBRACE

// indexing operator can be applied to same expressions that can be called?
arrayIndex_expression ::= caller LBRACKET expression RBRACKET
arraySlice_expression ::= caller LBRACKET expression? COLON expression? RBRACKET
matrixIndex_expression ::= caller LBRACKET expression COMMA expression RBRACKET

tagStatement_expression ::= tagHead ( literal_expression | (LPAREN expressionsList? RPAREN))?
tagHead ::= HASH IDENTIFIER

forStatement ::= label? tagStatement_expression? forHead forBody
private label ::= IDENTIFIER COLON


forHead ::= forIn|forTraditional
forBody ::= block | forSingleStatement
forSingleStatement ::= DO statement

breakStatement ::= BREAK label?
continueStatement ::= CONTINUE label?

// Statements that are allowed in "blocks" like for loops, if statements, etc.
// A block is a sequence of statements contained in braces.
block ::= tagStatement_expression? LBRACE (statement)* RBRACE

statement ::= singleStatement (SEMICOLON singleStatement)*
private singleStatement ::= assignmentStatement
                       | procedureDeclarationStatement
                       | variableInitialization
                       | enumDeclarationStatement
                       | variableDeclarationStatement
                       | constantInitialization
                       | procedureOverloadStatement
                       | bitsetDeclarationStatement
                       | forStatement
                       | ifStatement
                       | whenStatement
                       | switchStatement
                       | deferStatement
                       | returnStatement
                       | breakStatement
                       | continueStatement
                       | attributeStatement
                       | usingStatement
                       | block
                       | expression

forTraditional ::= FOR (forStartStatement? SEMICOLON)? expression? (SEMICOLON forEndStatement?)?
forIn ::= FOR IDENTIFIER (COMMA IDENTIFIER)* IN expression

private forStartStatement ::=   variableInitialization | assignmentStatement | call_expression
private forEndStatement ::=   assignmentStatement | call_expression

ifStatement ::= IF  ifHead block (elseIfBlock)* (elseBlock)?
ifHead ::=  (variableInitialization (COMMA variableInitialization)* SEMICOLON)? expression
elseIfBlock ::= ELSE IF ifHead block
elseBlock ::= ELSE block

whenStatement ::= WHEN expression block (elseWhenBlock)* (elseBlock)?
elseWhenBlock ::= ELSE WHEN expression block

switchStatement ::= tagStatement_expression? SWITCH IN? ifHead? LBRACE (switchCaseBlock)* RBRACE
switchCaseBlock ::= CASE (caseExpression(COMMA caseExpression)*)? COLON (statement|FALLTHROUGH)*

caseExpression ::= expression
                  | typeDefinition_expression
                  | DOT IDENTIFIER

deferStatement ::= DEFER deferrableStatement
private deferrableStatement ::= assignmentStatement
                                | expression
                                | ifStatement
                                | forStatement
                                | switchStatement
                                | block
